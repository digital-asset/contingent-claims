-- Copyright (c) 2022 Digital Asset (Switzerland) GmbH and/or its affiliates. All rights reserved.
-- SPDX-License-Identifier: Apache-2.0

{-# OPTIONS -Wno-deprecations #-} -- To supress 'Monad' warnings

module ContingentClaims.Lifecycle (
    lifecycle
  , lifecycle'
  , acquire'
  , exercise
  , exercise'
  , expire
  , Result(..)
) where

import ContingentClaims.Claim (Claim(..), ClaimF(..), compare, Inequality(..))
import ContingentClaims.Observation qualified as Observation
import ContingentClaims.Util.Recursion (apoCataM)
import ContingentClaims.Util (pruneZeros', intrinsicAcquisitionTime)
import Daml.Control.Arrow ((|||),(&&&))
import Daml.Control.Recursion (project)
import Daml.Control.Monad.Trans.Writer (WriterT, runWriterT)
import Daml.Control.Monad.Writer.Class (MonadWriter(..))
import Daml.Control.Monad.Trans.Class (lift)
import DA.List (singleton)
import DA.Traversable (sequence)
import DA.Foldable (elem, foldMap)
import DA.Optional (fromSomeNote)
import Prelude hiding (sequence, mapA, exercise, compare, elem)

type C t a = Claim t Decimal a
type F t a = ClaimF t Decimal a

-- | Returned from a `lifecycle` operation.
data Result t a = Result with
  pending : [(t, Decimal, a)]
    -- ^ quantity/asset pairs requiring settlement.
  remaining : C t a
    -- ^ the tree after lifecycled branches have been pruned.
    deriving (Eq, Show)

-- | Collect claims falling due into a list, and return the tree with those nodes pruned.
-- `m` will typically be `Update`. It is parametrised so it can be run in a `Script`.
-- The first argument is used to lookup the value of any `Observables`.
-- Results in a function taking today's date, and returning the pruned tree + pending settlements.
lifecycle : (Ord t, Eq a, CanAbort m)
  => (a -> t -> m Decimal)
  -- ^ function to evaluate observables 
  -> C t a
  -- ^ the input claim
  -> t
  -- ^ the today's date
  -> m (Result t a)
lifecycle spot claim today
  = fmap (uncurry $ flip Result)
  . runWriterT
  . apoCataM pruneZeros' acquireThenSettle
  . (1.0, ) -- initial scaling factor
  $ (acquisitionTime, claim)
  where
    acquireThenSettle =
      (lifecycle' spot =<<)
      . lift
      . sequence
      . fmap (sequence . (Prelude.fst &&& acquire' spot today))
    acquisitionTime = fromSomeNote "Claim does not have an intrinsic acquisition time" $ intrinsicAcquisitionTime claim

-- | Helper type used to write apomorphisms on a claim. `Left` is used if unfolding shall not continue. Otherwise, `Right` is used.
-- The generic type parameter `x` is the carrier of the R-CoAlgebra (typically the sub-tree as well as some additional information).
type FE t a x = F t a (Either (C t a) x)

-- | Acquired claim (in functor form). The acquisition time is attached to acquired sub-trees.
type Acquired t a = FE t a (t, C t a)

-- | Evaluate observables and skip branches for which predicates don't hold, calculate the acquisition time of the claim's sub-trees.
-- Consume `Cond` nodes, leaving the relevant sub-tree.
-- Replace `When pred c` nodes with `When (TimeGte t) c` if the predicate is non-deterministic and evaluates to True.
-- This is useless on its own. Composed with other functions, it adds laziness.
acquire' : (Ord t, Eq a, CanAbort m)
  => (a -> t -> m Decimal)
  -- ^ function to evaluate observables 
  -> t
  -- ^ the today date
  -> (t, C t a)
  -- ^ input claim in functor form and its acquisition time
  -> m (Acquired t a)
acquire' spot t (s, When obs c) = do
  predicate <- compare spot obs t
  if predicate then
    case obs of
      TimeGte sNew -> pure . WhenF obs $ Right (max s sNew, c)
      other -> pure . WhenF (TimeGte t) $ Right (t, c)  -- if the predicate is non-deterministic, we assume that we are lifecycling at a time `t` corresponding to the first instant the predicate becomes True
    else
      pure . WhenF obs $ Left c
acquire' spot today (s, Cond obs c c') = do
  predicate <- compare spot obs s
  acquire' spot today . (s, ) $ if predicate then c else c'
acquire' spot today (_, Anytime obs c) = do
  predicate <- compare spot obs today
  pure . AnytimeF obs $ if predicate then Right (today,c) else Left c  -- the acquisition time of an Anytime node is assumed to be the time at which we lifecycle
acquire' _ _ (s, other) = pure $ Right . (s, ) <$> project other

-- | Carrier type used for `lifecycle`. It consists of a claim, its acquisition time and the accumulated scaling factor.
type LifecycleCarrier t a = (Decimal, (t, C t a))

-- | Evaluate any observables in `Scale` nodes, accumulating scale factors top-down.
-- Log the scale factors with their corresponding leaf values. 
-- Stop recursion at `Or` and `Anytime` branches, guaranteeing liveness.
-- Replace any Ones that can be reached with Zeros.
lifecycle' : (Ord t, CanAbort m)
        => (a -> t -> m Decimal)
        -> (Decimal, (t, Acquired t a))
        -- ^ the acquired input claim, its acquisition time and the accumulated scaling factor
        -> WriterT [(t, Decimal,a)] m (FE t a (LifecycleCarrier t a))
lifecycle' _ (qty, (s, OneF asset)) = do
  tell [(s, qty, asset)]
  pure $ ZeroF
lifecycle' _ (qty, (_, GiveF c)) =
  pure . GiveF $ (-qty, ) <$> c
lifecycle' spot (qty, (s, ScaleF obs c)) = do
  k <- lift $ Observation.eval spot obs s
  pure . ScaleF obs $ (k * qty, ) <$> c
lifecycle' _ (_, (_, c@OrF{})) =
  pure $ stop c
  where stop = fmap $ Left . (identity ||| Prelude.snd)
lifecycle' _ (_, (_, c@(AnytimeF _ _))) =
  pure $ stop c
  where stop = fmap $ Left . (identity ||| Prelude.snd)
lifecycle' _ (qty, (_, other)) = pure $ fmap (qty, ) <$> other

--  | Acquire `Anytime` and `Or` nodes, by making an election.
-- `import` this `qualified`, to avoid clashes with `Prelude.exercise`.
exercise : (Ord t, Eq a, CanAbort m)
  => (a -> t -> m Decimal)
  -- ^ function to evaluate observables 
  -> (Bool, C t a)
  -- ^ the election being made
  -> C t a
  -- ^ the input claim
  -> t
  -- ^ the election date
  -> m (C t a)
exercise spot election claim today = 
  apoCataM pruneZeros' acquireThenExercise
  . (True, ) -- initial election authorizer (`True = bearer`)
  $ (acquisitionTime, claim)
  where
    acquireThenExercise =
      fmap (exercise' election today)
      . sequence
      . fmap (sequence . (Prelude.fst &&& acquire' spot today))
    acquisitionTime = fromSomeNote "Claim does not have an intrinsic acquisition time" $ intrinsicAcquisitionTime claim

-- | Carrier type used for `exercise`. It consists of a claim, its acquisition time and a flag keeping track of who is the entitled to the election (`True = bearer`).
type ExerciseCarrier t a = (Bool, (t, C t a))

-- Resolve `Or` nodes by removing them (keeping elected subtrees).
-- Fix acquisition time of exercised `Anytime` nodes by replacing them with `When (TimeGte t)`.
-- The election consists of a boolean representing the authorizer (`True = bearer`),
-- and this is compared against available branches of the choice.
exercise' : (Eq t, Eq a)
  => (Bool, C t a)
  -- ^ the election being made
  -> t
  -- ^ the election date
  -> (Bool, (t, Acquired t a))
  -- ^ the acquired claim, its acquisition time and a flag keeping track of who is the entitled to the election (`True = bearer`)
  -> FE t a (ExerciseCarrier t a)
exercise' _ _ (isBearer, (_, GiveF c)) = GiveF $ (not isBearer, ) <$> c
exercise' (elector, election) t (isBearer, (s, ors@OrF {}))
  | elector /= isBearer = stop ors
  | (t, election) `elem` elections = Left <$> project election
  | otherwise = stop ors
  where stop = fmap $ Left . (identity ||| Prelude.snd)
        elections = foldMap (foldMap singleton) ors
exercise' (elector, election) t (isBearer, (s, f@(AnytimeF _ _)))
  | elector /= isBearer = stop f
  | (t, election) `elem` elections = Left <$> WhenF (TimeGte t) election
  | otherwise = stop f
  where stop = fmap $ Left . (identity ||| Prelude.snd)
        elections = foldMap (foldMap singleton) f
exercise' _ _ (isBearer, (_, other)) = fmap (isBearer, ) <$> other

-- | Replace any subtrees that have expired with `Zero`s.
expire : (Ord t, Eq a, CanAbort m) => (a -> t -> m Decimal) -> C t a -> t -> m (C t a)
expire spot claim today =
  apoCataM pruneZeros' acquireThenExpire
  $ (acquisitionTime, claim)
  where
    acquireThenExpire = 
      (expire' spot today =<<)
      . sequence . (Prelude.fst &&& acquire' spot today)
    acquisitionTime = fromSomeNote "Claim does not have an intrinsic acquisition time" $ intrinsicAcquisitionTime claim

-- | Carrier type used for `expire`. It consists of a claim and its acquisition time.
type ExpireCarrier t a = (t, C t a)

-- | HIDE
-- Replace Until nodes with Zero when applicable
expire' : (Ord t, Monad m) => (a -> t -> m Decimal) -> t -> (t, Acquired t a) -> m (FE t a (ExpireCarrier t a))
expire' spot t (s, UntilF obs c) = do
  predicate <- compare spot obs t
  if predicate then pure ZeroF else pure $ UntilF obs c
expire' _ _ (s, other) = pure other
