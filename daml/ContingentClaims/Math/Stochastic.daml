-- Mathematical expression, derived from `Claim`, used for pricing

module ContingentClaims.Math.Stochastic where

import ContingentClaims.Claim
import ContingentClaims.Observable (Inequality(..))
import ContingentClaims.Observation qualified as O
import Daml.Control.Recursion
import Daml.Control.Arrow ((|||))
import Prelude hiding (Time)

type Time = Decimal
data Process = Process { dt : Expr, dW: Expr } deriving (Show, Eq)-- Geometric i.e. dX / X = α dt + β dW, so we have closed solns.
-- TODO: make sum type and support Poisson

riskless r = Process { dt = Ident r, dW = Const 0.0 }

gbm μ σ = Process { dt = Ident μ, dW = Ident σ }

data ExprF x
  = ConstF Decimal
  | IdentF Text
  | ProcF { name : Text, process: Process , filtration: Time }
  | SupF { f : (), lowerBound: Time }
  | AddF { lhs: x, rhs: x }
  | NegF x
  | MulF { lhs: x, rhs: x }
  | PowF { lhs: x, rhs: x }
  | I_F { lhs : x, rhs: x }
  | E_F { rv : x, filtration: Time }
  deriving (Functor)

data Expr
  = Const Decimal
  | Ident Text
  | Proc { name : Text, process : Process , filtration: Time }
  | Sup { f : (), lowerBound: Time }
  | Add (Expr, Expr)
  | Neg (Expr)
  | Mul (Expr, Expr)
  | Pow (Expr, Expr)
  | I (Expr , Expr)
  | E { rv : Expr, filtration: Time }
  deriving (Show, Eq)

instance Recursive (Expr) (ExprF) where
  project (Const x) = ConstF x
  project (Ident x) = IdentF x
  project Proc{..} = ProcF with ..
  project Sup{..} = SupF with ..
  project (Add (x, x')) = AddF x x'
  project (Neg x) = NegF x
  project (Mul (x,x')) = MulF x x'
  project (Pow (x, x')) = PowF x x'
  project (I (x, x')) = I_F x x'
  project E{..} = E_F with ..

instance Corecursive (Expr) (ExprF) where
  embed (ConstF x) = Const x
  embed (IdentF x) = Ident x
  embed ProcF{..} = Proc with ..
  embed SupF{..} = Sup with ..
  embed (AddF x x') = Add (x, x')
  embed (NegF x) = Neg x
  embed (MulF x x') = Mul (x, x')
  embed (PowF x x') = Pow (x, x')
  embed (I_F x x') = I (x, x')
  embed E_F{..} = E with ..

-- | Converts a `Claim` into the Fundamental Asset Pricing Formula. The ϵ
-- expressions are defined as E1-E10 in the Eber/Peyton-Jones paper. If you
-- squint you can almost see they correspond one-to-one to the formulae in our
-- whitepaper.
fapf : (Eq a, Show a)
     => a
     -> (a -> Process)
     -> (a -> a -> Process)
     -> Claim O.Observation Time Decimal a -> Expr
fapf ccy disc exch = futu coalg . Left . (, 0.0) where
  -- coalg : (Either (Claim, t) Observable) -> Free ExprF (Either (Claim, t) Observable)
  coalg = ϵ ||| υ
  -- ϵ : (Claim, t) -> Free ExprF (Either (Claim, t) Observable)
  ϵ (Zero, _) = ConstF zero
  ϵ (One asset, t) = exch' asset ccy -- assert ident
  ϵ (Give c, t) = NegF . claim $ (c, t)
  ϵ (Scale k c, t) = obs k `MulF` claim (c, t)
  ϵ (And c c', t) = claim (c, t) `AddF` claim (c', t)
  ϵ (Or c c', t) = AddF
      (ind (claim (c , t)) (claim (c', t)) * claim (c , t))
      (ind (claim (c', t)) (claim (c , t)) * claim (c', t)) -- How to distinguish E6/E7?
  ϵ (Cond (TimeGte t') c c', t) = AddF
      (ind (claim (c , t)) (claim (c', t)) * claim (c', t))
      (ind (claim (c', t)) (claim (c , t)) * claim (c , t)) -- How to distinguish E6/E7?
  ϵ (When (TimeGte t') c, t) = MulF
      (disc' ccy t)
      $ ex (claim (c, t') / disc' ccy t') t
  ϵ (Anytime (TimeGte t') c, t) = error "fapf: Anytime"
  ϵ (Until (TimeGte t') c, t) = error "fapf: Until"
  ϵ _ = error "fapf: Stochastic 'Indicator = Lte' functions not supported yet"
  -- υ : Observable -> ExprF Observable
  υ (O.Const {value=k}) = ConstF k
  υ (O.Observe {key=asset}) = exch' asset ccy
  υ (O.Add (x, x')) = AddF (obs x) (obs x')
  υ (O.Neg x) = NegF (obs x)
  υ (O.Mul (x, x')) = MulF (obs x) (obs x')
  υ (O.Div (x, x')) = MulF (obs x) (inv (obs x'))

  -- these operators are sugar for writing the futumorphism using `Free`, above
  x * y = Impure $ MulF x y
  x / y = Impure $ MulF x (inv y)
  inv x = Impure $ PowF x (obs . O.Neg . O.Const $ one)
  ind x y = Impure $ I_F x y
  ex x t = Impure $ E_F x t
  claim = Pure . Left
  obs = Pure . Right
  disc' k t = Impure $ ProcF (show k) (disc k) t
  exch' asset ccy = if asset == ccy then ConstF one else ProcF (show asset) (exch asset ccy) 0.0  -- FIXME: does it matter this is set to zero?
  one = munit
  zero = aunit

simplify : Expr -> Expr
simplify = {-cata unitIdentity . cata zeroIdentity . cata factNeg .-}  
    ana (commuteLeft . embed . distAdd) 
  . ana commuteLeft 
  . cata mulBeforeAdd

zeroIdentity : ExprF Expr -> Expr
zeroIdentity (MulF (Const 0.0) x) = Const 0.0
zeroIdentity (MulF x (Const 0.0)) = Const 0.0
zeroIdentity (PowF x (Const 0.0)) = Const 1.0
zeroIdentity (AddF (Const 0.0) x) = x
zeroIdentity (AddF x (Const 0.0)) = x
zeroIdentity (E_F (Const 0.0) _) = Const 0.0
zeroIdentity other = embed other

unitIdentity : ExprF Expr -> Expr
unitIdentity (MulF (Const 1.0) x) = x
unitIdentity (MulF x (Const 1.0)) = x
unitIdentity (PowF x (Const 1.0)) = x
unitIdentity other = embed other

factNeg : ExprF Expr -> Expr
factNeg (NegF (Neg x)) = x
factNeg (MulF (Neg x) (Neg y)) = Mul (x, y)
factNeg (MulF (Neg x) y) = Neg $ Mul (x, y)
factNeg (MulF y (Neg x)) = Neg $ Mul (y, x)
factNeg (E_F (Neg x) t) = Neg $ E x t
factNeg other = embed other

distAdd : Expr -> ExprF Expr
distAdd (Mul (x, Add (a, b))) = Mul (x, a) `AddF` Mul (x, b)
distAdd other = project other

-- Change `(a + b) x c` to `c x (a + b)`
mulBeforeAdd : ExprF Expr -> Expr
mulBeforeAdd (MulF y@Add{} x) = Mul (x, y)
mulBeforeAdd (MulF (Mul (x, y@Add{})) x') = Mul (Mul (x,x'), y)
mulBeforeAdd other = embed other

-- Change e.g. `a x (b x c)` to `(a x b) x c`
commuteLeft : Expr -> ExprF Expr
commuteLeft (Mul (x,(Mul (a, b)))) = Mul (x, a) `MulF` b
commuteLeft (Add (x,(Add (a, b)))) = Add (x, a) `AddF` b
commuteLeft other = project other

{-
distAdd : Expr -> ExprF Expr
distAdd (Mul (Add (x, x'), y)) = Mul (x, y) `AddF` Mul (x', y)
distAdd (Mul (y, Add (x, x'))) = Mul (y, x) `AddF` Mul (y, x')
distAdd (Div (Add (x, x'), y)) = Div (x, y) `AddF` Div (x', y)
distAdd (E (Add (x, x')) t) = E x t `AddF` E x' t
distAdd (Div (Mul (x, x'), y)) = Div (x, y) `MulF` x'
distAdd other = project other
-}
