-- Mathematical expression, derived from `Claim`, used for pricing

module ContingentClaims.Math.Stochastic where

import ContingentClaims.Claim
import ContingentClaims.Observable (Inequality(..))
import ContingentClaims.Observation qualified as O
import Daml.Control.Recursion
import Daml.Control.Arrow ((|||))
import Prelude hiding (Time)

type Time = Decimal
type Variable θ = θ -> Decimal
type Process θ = Time -> Variable θ

instance Show (Process θ) where
  show _ = "S(t, θ)"

data ExprF θ x
  = ProcF (Process θ)
  | SupF { f : Time -> x, lowerBound: Time }
  | AddF { lhs: x, rhs: x }
  | NegF x
  | MulF { lhs: x, rhs: x }
  | DivF { lhs: x, rhs: x }
  | I_F { lhs : x, rhs: x }
  | E_F { rv : x, filtration: Time }
  deriving (Functor)

data Expr θ
  = Proc (Process θ)
  | Sup { f : Time -> Expr θ, lowerBound: Time }
  | Add (Expr θ, Expr θ)
  | Neg (Expr θ)
  | Mul (Expr θ, Expr θ)
  | Div (Expr θ, Expr θ)
  | I (Expr θ , Expr θ)
  | E { rv : Expr θ, filtration: Time }

instance Corecursive (Expr θ) (ExprF θ) where
  embed = undefined

-- | Converts a `Claim` into the Fundamental Asset Pricing Formula. The ϵ
-- expressions are defined as E1-E10 in the Eber/Peyton-Jones paper. If you
-- squint you can almost see they correspond one-to-one to the formulae in our
-- whitepaper.
fapf : Eq a
     => a
     -> (a -> Time -> Process θ)
     -> (a -> a -> Process θ)
     -> θ
     -> Claim O.Observation Time Decimal a -> Expr θ
fapf ccy disc exch theta = futu coalg . Left . (, 0.0) where
  -- coalg : (Either (Claim, t) Observable) -> Free ExprF (Either (Claim, t) Observable)
  coalg = ϵ ||| υ
  -- ϵ : (Claim, t) -> Free ExprF (Either (Claim, t) Observable)
  ϵ (Zero, _) = ProcF . const . const $ zero
  ϵ (One asset, _) = ProcF $ exch asset ccy
  ϵ (Give c, t) = NegF . claim $ (c, t)
  ϵ (Scale k c, t) = obs k `MulF` claim (c, t)
  ϵ (And c c', t) = claim (c, t) `AddF` claim (c', t)
  ϵ (Or c c', t) = AddF
      (ind (claim (c , t)) (claim (c', t)) * claim (c , t))
      (ind (claim (c', t)) (claim (c , t)) * claim (c', t)) -- How to differentiate E6/E7?
  ϵ (Cond (TimeGte t') c c', t) = AddF
      (ind (claim (c , t)) (claim (c', t)) * claim (c', t))
      (ind (claim (c', t)) (claim (c , t)) * claim (c , t)) -- How to differentiate E6/E7?
  ϵ (When (TimeGte t') c, t) = MulF
      (disc' ccy t)
      $ ex (claim (c, t') / disc' ccy t') t
  ϵ (Anytime (TimeGte t') c, t) = error "fapf: Anytime"
  ϵ (Until (TimeGte t') c, t) = error "fapf: Until"
  ϵ _ = error "fapf: Stochastic 'Indicator = Lte' functions not supported yet"
  -- υ : Observable -> ExprF Observable
  υ (O.Const {value=k}) = ProcF . const . const $ k
  υ (O.Observe {key=asset}) = ProcF $ exch asset ccy
  υ (O.Add (x, x')) = AddF (obs x) (obs x')
  υ (O.Neg x) = MulF (Impure . ProcF . const . const . negate $ one) (obs x)
  υ (O.Mul (x, x')) = MulF (obs x) (obs x')
  υ (O.Div (x, x')) = DivF (obs x) (obs x')

  -- these operators are sugar for writing the futumorphism using `Free`, above
  x * y = Impure $ MulF x y
  x / y = Impure $ DivF x y
  ind x y = Impure $ I_F x y
  ex x t = Impure $ E_F x t
  claim = Pure . Left
  obs = Pure . Right
  disc' k t = Impure . ProcF $ disc k t
  zero = aunit
  one = munit

-- test = script do
